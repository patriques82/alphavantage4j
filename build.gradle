repositories {
    jcenter()
    mavenCentral()
}

buildscript {
    ext {
        project_name = project.name
        project_group = 'org.patriques'
        project_version = 1.2
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.jfrog.bintray.gradle', name: 'gradle-bintray-plugin', version: '1.7.3'
    }
}

group project_group
version project_version

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

apply plugin: 'eclipse'
apply plugin: 'idea'

apply plugin: 'com.jfrog.bintray'

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes('Implementation-Title': project_name, 'Implementation-Version': project_version)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project_group
            artifactId project_name
            version project_version
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

bintray {
    user = System.getenv('bintray_user') ?: bintray_user
    key = System.getenv('bintray_key') ?: bintray_key
    publish = true //If version should be auto published after an upload
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project_name
        websiteUrl = "https://github.com/patriques82/$project_name"
        vcsUrl = "https://github.com/patriques82/$project_name" + ".git"

        githubRepo = "patriques82/$project_name"
        githubReleaseNotesFile = 'README.md'
        issueTrackerUrl = "https://github.com/patriques82/$project_name/issues"

        licenses = ['Apache-2.0']
        version {
            name = project_version
            vcsTag = project_version
            desc = 'A Java wrapper to get stock data and stock indicators from the Alpha Vantage API'
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}



